import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'com.yakovliam'
version '1.0.0-SNAPSHOT'
project.ext.name = 'ChatGames'
project.ext.groupId = 'com.yakovliam.chatgames'

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = 'https://repo.mattstudios.me/artifactory/public/' }
    maven { url = "https://repo.triumphteam.dev/snapshots" }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.piggypiglet.me/releases' }
}

dependencies {
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-api:4.10.1'
    implementation 'net.kyori:adventure-platform-bukkit:4.1.0'
    implementation 'net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT'
    implementation 'me.lucko:commodore:1.11'
    implementation 'dev.triumphteam:triumph-gui:3.1.2'
    implementation 'co.aikar:acf-bukkit:0.5.0-SNAPSHOT'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.6'
    implementation 'me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT'
    implementation 'me.mattstudios:triumph-msg:2.2.4-SNAPSHOT'
    implementation 'org.spongepowered:configurate-gson:4.1.2'
    implementation 'me.piggypiglet:TimeAPI:+'
}

// Output to build/libs/shadow.jar
shadowJar {
    archiveBaseName.set("${project.ext.name}-${project.version}")
    archiveClassifier.set('')
    archiveVersion.set('')

    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    relocate "dev.triumphteam.gui", "${project.ext.groupId}.lib.triumphgui"
    relocate 'me.mattstudios.msg', "${project.ext.groupId}.lib.msg"
    relocate "net.kyori", "${project.ext.groupId}.lib.kyori"
    relocate "com.github.benmanes.caffeine", "${project.ext.groupId}.lib.caffeine"
    relocate "me.lucko.commodore", "${project.ext.groupId}.lib.commodore"
    relocate "co.aikar.commands", "${project.ext.groupId}.lib.acf"
    relocate "sh.okx.timeapi", "${project.ext.groupId}.lib.timeapi"
}

// replace version information in src/main/resources
processResources {
    filter(ReplaceTokens, tokens: [version: version, name: project.ext.name])
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}